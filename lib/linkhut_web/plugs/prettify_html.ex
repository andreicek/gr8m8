defmodule LinkhutWeb.Plugs.PrettifyHtml do
  @behaviour Plug

  @moduledoc """
  Lots of what I've learned about HTML in my younger years was by looking at the source code of web pages.
  This plug pretty prints the HTML generated by Linkhut so that it's easy to read for humans without any particular tooling.
  """

  @doc false
  @impl true
  def init(opts \\ []), do: opts

  @doc false
  @impl true
  def call(conn, _opts) do
    Plug.Conn.register_before_send(conn, &prettify_body/1)
  end

  defp prettify_body(%Plug.Conn{status: status} = conn) when status in [200] do
    case List.keyfind(conn.resp_headers, "content-type", 0) do
      {_, "text/html" <> _} ->
        body =
          conn.resp_body
          |> to_string
          |> TidyEx.parse()

        %Plug.Conn{conn | resp_body: body}

      _ ->
        conn
    end
  end

  defp prettify_body(conn), do: conn
end
